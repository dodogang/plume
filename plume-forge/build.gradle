plugins {
    id "com.github.johnrengelman.shadow" version "5.0.0"
}

configurations {
    shadow
}

architectury {
    platformSetupLoomIde()
}

loom {
    mixinConfigs = [
            "plume.mixins.json",
            "ash.mixins.json"
    ]
    useFabricMixin = true
}

repositories {
    maven {
        name = "Progwml6 maven / JEI Maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        name = "ModMaven / JEI Mirror"
        url = "https://modmaven.k-4u.nl"
    }
}

dependencies {
    forge("net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge_version}")

    if (rootProject.hasProperty('jei_version')) {
        modCompile("mezz.jei:jei-${rootProject.jei_version}:api")
        modRuntime("mezz.jei:jei-${rootProject.jei_version}")
    }

    ext.dependOnModuleFromForge(":plume-common")
    ext.dependOnModuleFromForge(":ash-forge")
    ext.dependOnModuleFromForge(":ash-common")
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "fabric.mod.json"

    configurations = [project.configurations.shadow]
    classifier "shadow"
}

remapJar {
    dependsOn(shadowJar)
    input.set(shadowJar.archivePath)
    classifier "forge"
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifact(remapJar.archiveFile) {
                builtBy build
                classifier "forge"
            }
        }
    }
}